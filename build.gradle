buildscript {
    apply from: 'deps.gradle'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath deps.kotlin_plugin
        classpath deps.kotlin_kover
        classpath deps.spotlessPlugin
    }
}

apply plugin: 'kover'

repositories {
    mavenCentral()
}

subprojects { project ->
    group = GROUP
    version = VERSION_NAME

    repositories {
        mavenCentral()
    }

    apply plugin: 'maven-publish'
    apply plugin: 'com.diffplug.spotless'

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
    }

    spotless {
        kotlin {
            // optionally takes a version
            ktlint()
            target '**/*.kt'

            // also supports license headers
            licenseHeaderFile rootProject.file('spotless.license.java.txt')
        }
    }

    afterEvaluate {
        tasks.withType(Test) {
            testLogging {
                events "passed", "skipped", "failed"
            }
        }
    }
}
